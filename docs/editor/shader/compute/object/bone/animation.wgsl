import ArmatureAllocation;

struct Bone {
    position: vec2<f32>,
    scale: vec2<f32>,
    angle: f32,
    length: f32,
}

@group(0) @binding(0) var<storage, read_write> localBonewMatrix: array<f32>; // 出力
@group(0) @binding(1) var<storage, read> base: array<Bone>; // 元
@group(0) @binding(2) var<storage, read> animations: array<Bone>; // アニメーション
@group(0) @binding(3) var<storage, read> armatureAllocations: array<ArmatureAllocation>; // 配分

// 2次元の回転、スケール、平行移動を表現する行列を作成する関数
fn createTransformMatrix(scale: vec2<f32>, angle: f32, translation: vec2<f32>) -> mat3x3<f32> {
    let rx = angle;
    let ry = angle + 1.5708;
    // スケールと回転を組み合わせた行列
    var matrix: mat3x3<f32>;
    matrix[0] = vec3<f32>(scale.x * cos(rx), scale.x * sin(rx), 0.0);
    matrix[1] = vec3<f32>(scale.y * cos(ry), scale.y * sin(ry), 0.0);
    matrix[2] = vec3<f32>(translation.x, translation.y, 1.0);

    return matrix;
}

fn setMatrix(index: u32, m: mat3x3<f32>) {
    let fixIndex = index * 9u;
    localBonewMatrix[fixIndex] = m[0][0];
    localBonewMatrix[fixIndex + 1] = m[0][1];
    localBonewMatrix[fixIndex + 2] = m[0][2];
    localBonewMatrix[fixIndex + 3] = m[1][0];
    localBonewMatrix[fixIndex + 4] = m[1][1];
    localBonewMatrix[fixIndex + 5] = m[1][2];
    localBonewMatrix[fixIndex + 6] = m[2][0];
    localBonewMatrix[fixIndex + 7] = m[2][1];
    localBonewMatrix[fixIndex + 8] = m[2][2];
}

@compute @workgroup_size(8, 8)
fn main(@builtin(global_invocation_id) global_id: vec3<u32>) {
    let objectIndex = global_id.x;
    let vertexIndex = global_id.y;
    if (arrayLength(&armatureAllocations) <= objectIndex) { // オブジェクト数を超えているか
        return ;
    }
    if (armatureAllocations[objectIndex].bonesNum <= vertexIndex) { // ボーン数を超えているか
        return ;
    }

    let fixVertexIndex = armatureAllocations[objectIndex].bonesOffset + vertexIndex;
    var localBoneData = base[fixVertexIndex];
    let animation = animations[fixVertexIndex];
    localBoneData.position += animation.position;
    localBoneData.scale += animation.scale;
    localBoneData.angle += animation.angle;

    setMatrix(fixVertexIndex, createTransformMatrix(localBoneData.scale, localBoneData.angle, localBoneData.position));
}